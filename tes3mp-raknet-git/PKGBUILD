pkgname=tes3mp-raknet-git
pkgver=r108.9ace90a
pkgrel=1
pkgdesc="Cross platform C++ network library"
arch=('i686' 'x86_64')
url="https://github.com/TES3MP/RakNet.git"
license=('custom')
depends=('gcc-libs')
makedepends=('cmake')
provides=('raknet')
conflicts=('raknet' 'raknet-git' 'raknet-3.261')
source=("git+https://github.com/TES3MP/RakNet.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/RakNet"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare () {
  cd "${srcdir}/RakNet"

  # enable LIBCAT_SECURITY features
  #sed -i -e s@//#define@#define@g Source/NativeFeatureIncludesOverrides.h
  
  #add -fpermissive build option
  sed -i "s/project(RakNet)/project(RakNet)\r\nset(CMAKE_CXX_FLAGS \"-fpermissive -std=c++0x\")/" CMakeLists.txt
}

build()
{
  cd ${srcdir}/RakNet
  mkdir -p build && pushd build
  cmake -DCMAKE_BUILD_TYPE=Release \
    -DRAKNET_ENABLE_DLL=ON \
    -DRAKNET_ENABLE_SAMPLES=OFF \
    -DRAKNET_ENABLE_STATIC=ON \
    -DRAKNET_GENERATE_INCLUDE_ONLY_DIR=ON ..
  make 
}

package()
{
  cd ${srcdir}/RakNet

  # DESTDIR is not used correctly so copy library and its headers manually
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  mkdir "${pkgdir}"/usr/lib/
  mkdir "${pkgdir}"/usr/lib/raknet
  cp "${srcdir}"/RakNet/build/lib/libRakNetLibStatic.a "${pkgdir}"/usr/lib/raknet
  install -d "${pkgdir}"/usr/{lib,include}/raknet
  cp "${srcdir}"/RakNet/build/lib/libRakNetDLL.so "${pkgdir}"/usr/lib/
  install -m644 Source/*.h "${pkgdir}"/usr/include/raknet/
}
